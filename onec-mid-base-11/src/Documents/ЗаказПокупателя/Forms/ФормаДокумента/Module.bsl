
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// НачалоДоработки
	// Николаев В.Е. Задача 004 от 13.10.2024
	
	ПолеСогласованнаяСкидка = Элементы.Найти("СогласованнаяСкидка");
	
	Если НЕ ПолеСогласованнаяСкидка = Неопределено Тогда
		ПолеСогласованнаяСкидка.УстановитьДействие("ПриИзменении", "Дораб_СогласованнаяСкидкаПриИзменении");
	КонецЕсли;
	
	// КонецДоработки
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Асинх Процедура Дораб_СогласованнаяСкидкаПриИзменении(Элемент)
	
	// НачалоДоработки
	// Николаев В.Е. Задача 004 от 13.10.2024
	
	Сообщение = Новый СообщениеПользователю();
	
	Если Объект.Товары.Количество() > 0 Или Объект.Услуги.Количество() > 0 Тогда
		
		ТекстВопроса = "Пересчитать документ с учетом скидки?";
		
		ВариантОтвета = Новый СписокЗначений;
		ВариантОтвета.Добавить(1, "Да");
		ВариантОтвета.Добавить(2, "Нет");
		
		ОтветПользователя = Ждать ВопросАсинх(ТекстВопроса, ВариантОтвета);
		
		Если ОтветПользователя = 2 Тогда
			Возврат;
		ИначеЕсли ОтветПользователя = 1 тогда
			ПересчитатьТаблицу(Объект.Товары, Объект.Услуги);
			Сообщение.Текст = "Документ пересчитан";
			Сообщение.Сообщить();
		Иначе
			Сообщение.Текст = "Неизвестный вариант ответа";
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	// КонецДоработки
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	// НачалоДоработки
	// Николаев В.Е. Задача 004 от 13.10.2024
	
	// Исходный код	
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
	// Новый код
	
	КоэффициентСкидки = 1 - ((ТекущиеДанные.Скидка + Объект.Дораб_СогласованнаяСкидка) / 100);
	
	Если КоэффициентСкидки >= 0 Тогда
		ТекущиеДанные.Сумма = (ТекущиеДанные.Цена * ТекущиеДанные.Количество) * КоэффициентСкидки;
	Иначе
		ТекущиеДанные.Сумма = 0;
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = СтрШаблон("Суммарная скидка для номенклатуры: %1 превышает 100 процентов", ТекущиеДанные.Номенклатура);
		Сообщение.Поле = "ТекущиеДанные.Скидка";
		Сообщение.КлючДанных = Объект.Ссылка;
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	// КонецДоработки
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьТаблицу(ТаблицаТовары, ТаблицаУслуги)
	
	// НачалоДоработки
	// Николаев В.Е. Задача 004 от 13.10.2024

	Для Каждого Строка Из ТаблицаТовары Цикл
		РассчитатьСуммуСтроки(Строка);
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаУслуги Цикл
		РассчитатьСуммуСтроки(Строка);
	КонецЦикла;
	
	// КонецДоработки	
	
КонецПроцедуры


#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура КомандаПересчитать(Команда)
	
	// НачалоДоработки
	// Николаев В.Е. Задача 004 от 13.10.2024
	
	Сообщение = Новый СообщениеПользователю();
		
	Если Объект.Товары.Количество() > 0 Или Объект.Услуги.Количество() > 0 Тогда
		
		ПересчитатьТаблицу(Объект.Товары, Объект.Услуги);
		Сообщение.Текст = "Документ пересчитан с учетом согласованной скидки";
		Сообщение.Сообщить();
	Иначе
		
		Сообщение.Текст = "Нет данных для пересчета, необходимо ввести товар или услугу";
		Сообщение.Сообщить();
		Возврат;
		
	КонецЕсли;
	
	// КонецДоработки	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
